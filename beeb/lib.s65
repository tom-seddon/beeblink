;-------------------------------------------------------------------------
;
; BeebLink - BBC Micro file storage system
;
; Copyright (C) 2018, 2019, 2020 Tom Seddon
; 
; This program is free software: you can redistribute it and/or
; modify it under the terms of the GNU General Public License as
; published by the Free Software Foundation, either version 3 of the
; License, or (at your option) any later version.
; 
; This program is distributed in the hope that it will be useful, but
; WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
; General Public License for more details.
; 
; You should have received a copy of the GNU General Public License
; along with this program. If not, see
; <https://www.gnu.org/licenses/>.
;
;-------------------------------------------------------------------------

;-------------------------------------------------------------------------
; 
; Load word from memory and push onto stack.
;

push8 .macro
                lda \1
                pha
                .endm
                
push16 .macro
                push8 \1+1
                push8 \1+0
                .endm

push32 .macro
                push8 \1+3
                push8 \1+2
                push8 \1+1
                push8 \1+0
                .endm

push16zx: .macro
                lda 0,x
                pha
                lda 1,x
                pha
                .endm

push32zx: .macro
                lda 0,x
                pha
                lda 1,x
                pha
                lda 2,x
                pha
                lda 3,x
                pha
                .endm

;-------------------------------------------------------------------------
;
; Pop word from stack and store into memory.
;

pop8 .macro
                pla
                sta \1
                .endm
                
pop16 .macro
                pop8 \1
                pop8 \1+1
                .endm

pop32 .macro
                pop8 \1+0
                pop8 \1+1
                pop8 \1+2
                pop8 \1+3
                .endm

pop16zx: .macro
                pla
                sta 1,x
                pla
                sta 0,x
                .endm

pop32zx: .macro
                pla
                sta 3,x
                pla
                sta 2,x
                pla
                sta 1,x
                pla
                sta 0,x
                .endm
                
;-------------------------------------------------------------------------
;
; debug print nonsense.
;
; The macro's single argument is a list of items to print. Each item
; can be:
;
; string - string is printed as-is using oswrch
;
; 16-bit integer - print byte at that address, 2 hex digits
;
; DP_A, DP_X, DP_Y - print register, 2 hex digits
;
; DP_P - print P, 8 chars
;
; DP_BYTE(A) - print byte at address A, 2 hex digits (same as
; providing A as a 16-bit integer)
;
; DP_WORD(A) - print LE word at address A, 4 hex digits
;
; DP_DWORD(A) - print LE dword at address A, 8 hex digits
;
; DP_IND(A,D) - ldy #D:lda (A),y - print byte, 2 hex digits
;
; print - always prints using oswrch
; preserves: A; X; Y: P
;
; debug_print - prints using oswrch if the debug verbosity flag is set
; preserves: A; X; Y; P

DP_A=-1
DP_X=-2
DP_Y=-3
DP_P=-4

DP_IND_TYPE=-1
DP_WORD_TYPE=-2
DP_DWORD_TYPE=-3
DP_BYTE_TYPE=-4

DP_BYTE: .sfunction _address,(DP_BYTE_TYPE,_address)
DP_IND: .sfunction _address,offset,(DP_IND_TYPE,_address,offset)
DP_WORD: .sfunction _address,(DP_WORD_TYPE,_address)
DP_DWORD: .sfunction _address,(DP_DWORD_TYPE,_address)

print: .macro items
                php             ;104=p
                pha             ;103=a
                txa
                pha             ;102=x
                tya
                pha             ;101=y
                tsx
                .for _i=0,_i<len(\items),_i+=1

                .if type(\items[_i])==str||type(\items[_i])==bytes

                .if len(\items[_i])==1
                lda #\items[_i][0]
                jsr oswrch
                .elsif \items[_i]=='\r\n' || \items[_i]=='\n\r'
                jsr osnewl
                .else
                jsr pcprint
                .text \items[_i],255
                .endif
                
                .elsif type(\items[_i])==int||type(\items[_i])==bits||type(\items[_i])==code
                
                .if \items[_i]>=0&&\items[_i]<=65535
                lda \items[_i]
                jsr print_hex_byte
                .elsif \items[_i]==DP_A
                lda $103,x
                jsr print_hex_byte
                .elsif \items[_i]==DP_X
                lda $102,x
                jsr print_hex_byte
                .elsif \items[_i]==DP_Y
                lda $101,x
                jsr print_hex_byte
                .elsif \items[_i]==DP_P
                lda $104,x
                jsr print_p
                .else
                .cerror true,"bad debug_print integer"
                .endif

                .elsif type(\items[_i])==tuple
                
                .if \items[_i][0]==DP_IND_TYPE
                ldy #\items[_i][2]
                lda (\items[_i][1]),y
                jsr print_hex_byte
                .elsif \items[_i][0]==DP_BYTE_TYPE
                lda \items[_i][1]
                jsr print_hex_byte
                .elsif \items[_i][0]==DP_WORD_TYPE
                .if \items[_i][1]>=0 && \items[_i][1]<=254
                ldx #\items[_i][1]
                jsr debug_print_hex.word
                .else
                lda \items[_i][1]+1
                jsr print_hex_byte
                lda \items[_i][1]+0
                jsr print_hex_byte
                .endif
                .elsif \items[_i][0]==DP_DWORD_TYPE
                .if \items[_i][1]>=0 && \items[_i][1]<=252
                ldx #\items[_i][1]
                jsr debug_print_hex.dword
                .else
                ldx #3
-
                lda @w \items[_i][1],x
                jsr print_hex_byte
                dex
                bpl -
                tsx
                .endif
                .else
                .error "bad debug_print tuple"
                .endif
                
                .else

                .cerror true,"bad debug_print item: ",\items[_i]," ",type(\items[_i])

                .endif
                
                .next

                pla
                tay
                pla
                tax
                pla
                plp
                .endm

;-------------------------------------------------------------------------
;
;

debug_print .macro items,enabled=true
                .if enable_debug_print
                .if \enabled
                php             ;104=p
                jsr get_vstatus
                bcc +
                print list(\items)
+
                plp
                .endif
                .endif
                .endm
                
;-------------------------------------------------------------------------
;
;
;

strings_list .macro strings

addresses:=[]
strings:
                .for i=0,i<len(\strings),i+=1
addresses:=addresses..[*]
                .text \strings[i],255
                .next

l:
                .byte <addresses
h:
                .byte >addresses
                .endm

;-------------------------------------------------------------------------

branch_same_page .macro target
                .cerror ((*+2)&$ff00)!=(\target&$ff00),"branch crosses page"
                .endm
                
beq_same_page .macro target
                .branch_same_page \target
                beq \target
                .endm

bne_same_page .macro target
                .branch_same_page \target
                bne \target
                .endm

bmi_same_page .macro target
                .branch_same_page \target
                bmi \target
                .endm

bpl_same_page .macro target
                .branch_same_page \target
                bpl \target
                .endm

bcc_same_page .macro target
                .branch_same_page \target
                bcc \target
                .endm

bcs_same_page .macro target
                .branch_same_page \target
                bcs \target
                .endm

bvc_same_page .macro target
                .branch_same_page \target
                bvc \target
                .endm

bvs_same_page .macro target
                .branch_same_page \target
                bvs \target
                .endm

;-------------------------------------------------------------------------

                .section lib_code
debug_print_hex: .proc
dword:
                lda 3,x
                jsr print_hex_byte
                lda 2,x
                jsr print_hex_byte
word:
                lda 1,x
                jsr print_hex_byte
                lda 0,x
                tsx
                jmp print_hex_byte
                .pend

;-------------------------------------------------------------------------
;
;
toupper: .proc
                cmp #'a'
                bcc done
                cmp #'z'+1
                bcs done
                and #$df
done:
                rts
                .pend


;-------------------------------------------------------------------------
;
; Add a small constant value to a 16-bit value in zero page.
;
; entry: X = zero page address of 16-bit value
; exit: Z set if value became zero
; preserves: A; X; Y; C
;
add3z16: .proc
                jsr add1z16
                jsr add1z16
                .pend
add1z16: .proc
                inc 0,x
                bne done
                inc 1,x
done:
                rts
                .pend

;-------------------------------------------------------------------------
;
; Add Y to a 16-bit value in zero page.
;
; entry: X = zero page address of 16-bit value
;        Y = value to add
; preserves: A; X; Y; P
;
; TODO - does this really need to preserve anything any more?
;
addyz16: .proc
                php
                pha
                clc
                tya
                adc 0,x
                sta 0,x
                bcc addyz16_done
                inc 1,x
addyz16_done:
                pla
                plp
                rts
                .pend

;-------------------------------------------------------------------------
;
; Calls OSBYTE with X=0 and Y=255
;
; entry: A = OSBYTE to call
; exit: as per whichever OSBYTE it was
; 
osbyte_x00_yff: .proc
                ldx #0
                ldy #$ff
                jmp osbyte
                .pend

;-------------------------------------------------------------------------
;
; Print string pointed to by Y/X.
;
; entry: YX = points to string
;
printyx: .proc
                .push16 print_ptr
                stx print_ptr+0
                sty print_ptr+1
                jsr print_print_ptr
                .pop16 print_ptr
                rts
                .pend

;-------------------------------------------------------------------------

print_print_ptr: .proc
                ldx #print_ptr
loop:
                lda (0,x)
                cmp #255
                beq done
                tay
                .push16zx
                tya
                jsr oswrch
                .pop16zx
                jsr add1z16
                jmp loop
done:
                rts
                .pend

;-------------------------------------------------------------------------

pcprint: .proc
                php
                pha
                txa
                pha
                tya
                pha
                .push16 print_ptr
                tsx
                lda $107,x
                sta print_ptr+0
                lda $108,x
                sta print_ptr+1
                ldx #print_ptr
                jsr add1z16
                jsr print_print_ptr
                tsx
                lda print_ptr+0
                sta $107,x
                lda print_ptr+1
                sta $108,x
                .pop16 print_ptr
                pla
                tay
                pla
                tax
                pla
                plp
                rts
                .pend

;-------------------------------------------------------------------------

print_hex_z32: .proc
                lda 3,x
                jsr print_hex_byte
                lda 2,x
                jsr print_hex_byte
                lda 1,x
                jsr print_hex_byte
                lda 0,x
                ; fall through
                .pend
                
;-------------------------------------------------------------------------

print_hex_byte: .proc
                pha
                jsr get_ascii_hi_nybble
                jsr oswrch
                pla
                pha
                jsr get_ascii_lo_nybble
                jsr oswrch
                pla
                rts
                .pend

;-------------------------------------------------------------------------

get_ascii_hi_nybble:
                lsr a
                lsr a
                lsr a
                lsr a
get_ascii_lo_nybble:
                and #$0f
                sed
                clc
                adc #$90
                adc #$40
                cld
                rts

;-------------------------------------------------------------------------

p_bit: .macro set,reset
                asl a
                pha
                .if \set==\reset
                lda #\set
                .else
                lda #\reset
                bcs +
                lda #\set
+
                .endif
                jsr oswrch
                pla
                .endm                
                
print_p: .proc
                p_bit "N","n"
                p_bit "V","v"
                p_bit "-","-"
                p_bit "B","b",
                p_bit "D","d"
                p_bit "I","i"
                p_bit "Z","z"
                p_bit "C","c",
                rts
                
                .pend

;-------------------------------------------------------------------------

                .endsection lib_code

;-------------------------------------------------------------------------

make_request_osword_block: .struct request,request_addr,request_size,response_addr,response_size
input_size: .byte 22;\block_size
output_size: .byte 22;\block_size
request: .byte \request
response: .fill 1
flags: .fill 1
fs_id: .fill 1                
request_addr: .dword \request_addr
request_size: .dword \request_size
response_addr: .dword \response_addr
response_size: .dword \response_size
                .endstruct
                
;-------------------------------------------------------------------------
